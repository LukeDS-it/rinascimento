buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'http://ldsoftware.it:8081/nexus/content/repositories/releases/' }
        maven { url 'http://ldsoftware.it:8081/nexus/content/repositories/snapshots/' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}")
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'war'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'http://ldsoftware.it:8081/nexus/content/repositories/releases/' }
    maven { url 'http://ldsoftware.it:8081/nexus/content/repositories/snapshots/' }
}

configurations {
    providedRuntime
}

dependencies {
    compile "org.codehaus.groovy:groovy"
    compile "org.springframework.boot:spring-boot-starter-web:${spring_boot_version}"
    compile "org.springframework.boot:spring-boot-starter-data-jpa:${spring_boot_version}"
    compile "org.springframework.boot:spring-boot-configuration-processor:${spring_boot_version}"
    compile 'org.thymeleaf:thymeleaf:3.0.9.RELEASE'

    compile project(':rinascimento-business')
    compile project(':rinascimento-view')

    compile "org.hibernate:hibernate-core:${hibernate_version}"

    runtime('com.h2database:h2')

    compile "org.springframework.boot:spring-boot-starter-tomcat:${spring_boot_version}"

    testCompile "org.springframework.boot:spring-boot-starter-test:${spring_boot_version}"
    testCompile('org.assertj:assertj-core:3.8.0')
}

/**
 * Copies the bundle generated from yarn installation into the main resources folder
 * so that the CMS can resolve them at runtime
 */
task moveWebApp(type: Copy) {
    from 'build/www/resources', 'build/www/cms-admin.html', 'build/www/cms-install.html'
    into 'build/resources/main'
}

/**
 * The war task must depend on the custom copy web app resources
 * task in order to add those resources correctly to the WAR file
 */
war {
    dependsOn moveWebApp
    archiveName = 'rinascimento.war'
}

bootRun {
    addResources = true
}

idea {
    module {
        excludeDirs += file "node_modules"
    }
}